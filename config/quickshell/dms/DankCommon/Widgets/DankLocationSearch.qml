import QtQuick
import qs.DankCommon.Common
import qs.DankCommon.Widgets

Item {
    id: root

    activeFocusOnTab: true

    KeyNavigation.tab: keyNavigationTab
    KeyNavigation.backtab: keyNavigationBacktab

    onActiveFocusChanged: {
        if (activeFocus) {
            locationInput.forceActiveFocus();
        }
    }

    property string currentLocation: ""
    property string placeholderText: I18n.tr("Search for a location...")
    property bool _internalChange: false
    property bool isLoading: false
    property string currentSearchText: ""
    property Item keyNavigationTab: null
    property Item keyNavigationBacktab: null

    signal locationSelected(string displayName, string coordinates)

    function resetSearchState() {
        locationSearchTimer.stop();
        dropdownHideTimer.stop();
        isLoading = false;
        searchResultsModel.clear();
    }

    // CJK city names are commonly two code points (北京, 東京, 서울).
    function canSearch(t) {
        return t.length > 2 || (t.length >= 2 && /[぀-ヿ㐀-䶿一-鿿豈-﫿가-힯]/.test(t));
    }

    width: parent.width
    height: searchInputField.height + (searchDropdown.visible ? searchDropdown.height : 0)

    ListModel {
        id: searchResultsModel
    }

    Timer {
        id: locationSearchTimer

        interval: 500
        running: false
        repeat: false
        onTriggered: {
            if (root.canSearch(locationInput.text)) {
                searchResultsModel.clear();
                root.isLoading = true;
                const searchLocation = locationInput.text;
                root.currentSearchText = searchLocation;
                const encodedLocation = encodeURIComponent(searchLocation);
                const searchUrl = "https://nominatim.openstreetmap.org/search?q=" + encodedLocation + "&format=json&limit=5&addressdetails=1";
                Proc.runCommand("locationSearch", [Proc.dmsBin, "dl", "-4", "--timeout", "10", searchUrl], (output, exitCode) => {
                    root.isLoading = false;
                    if (exitCode !== 0) {
                        searchResultsModel.clear();
                        return;
                    }
                    if (root.currentSearchText !== locationInput.text)
                        return;
                    const raw = output.trim();
                    searchResultsModel.clear();
                    if (!raw || raw[0] !== "[") {
                        return;
                    }
                    try {
                        const data = JSON.parse(raw);
                        if (data.length === 0) {
                            return;
                        }
                        for (var i = 0; i < Math.min(data.length, 5); i++) {
                            const location = data[i];
                            if (location.display_name && location.lat && location.lon) {
                                const parts = location.display_name.split(', ');
                                let cleanName = parts[0];
                                if (parts.length > 1) {
                                    const state = parts[parts.length - 2];
                                    if (state && state !== cleanName)
                                        cleanName += `, ${state}`;
                                }
                                const query = `${location.lat},${location.lon}`;
                                searchResultsModel.append({
                                    "name": cleanName,
                                    "query": query
                                });
                            }
                        }
                    } catch (e) {}
                });
            }
        }
    }

    Timer {
        id: dropdownHideTimer

        interval: 200
        running: false
        repeat: false
        onTriggered: {
            if (!locationInput.getActiveFocus() && !searchDropdown.hovered)
                root.resetSearchState();
        }
    }

    Item {
        id: searchInputField

        width: parent.width
        height: 48

        DankTextField {
            id: locationInput

            width: parent.width
            height: parent.height
            leftIconName: "search"
            placeholderText: root.placeholderText
            text: ""
            backgroundColor: Style.surfaceVariant
            normalBorderColor: Style.primarySelected
            focusedBorderColor: Style.primary
            keyNavigationTab: root.keyNavigationTab
            keyNavigationBacktab: root.keyNavigationBacktab
            onTextEdited: {
                if (root._internalChange)
                    return;
                if (getActiveFocus()) {
                    if (root.canSearch(text)) {
                        root.isLoading = true;
                        locationSearchTimer.restart();
                    } else {
                        root.resetSearchState();
                    }
                }
            }
            onFocusStateChanged: hasFocus => {
                if (hasFocus) {
                    dropdownHideTimer.stop();
                } else {
                    dropdownHideTimer.start();
                }
            }
        }

        DankIcon {
            name: root.isLoading ? "hourglass_empty" : (searchResultsModel.count > 0 ? "check_circle" : "error")
            size: Style.iconSize - 4
            color: root.isLoading ? Style.surfaceVariantText : (searchResultsModel.count > 0 ? Style.primary : Style.error)
            anchors.right: parent.right
            anchors.rightMargin: Style.spacingM
            anchors.verticalCenter: parent.verticalCenter
            opacity: (locationInput.getActiveFocus() && root.canSearch(locationInput.text)) ? 1 : 0

            Behavior on opacity {
                NumberAnimation {
                    duration: Style.shortDuration
                    easing.type: Style.standardEasing
                }
            }
        }
    }

    StyledRect {
        id: searchDropdown

        property bool hovered: false

        width: parent.width
        height: Math.min(Math.max(searchResultsModel.count * 38 + Style.spacingS * 2, 50), 200)
        y: searchInputField.height
        radius: Style.cornerRadius
        color: Style.withAlpha(Style.surfaceContainer, Style.popupTransparency)
        border.color: Style.primarySelected
        border.width: 1
        visible: locationInput.getActiveFocus() && root.canSearch(locationInput.text) && (searchResultsModel.count > 0 || root.isLoading)

        MouseArea {
            anchors.fill: parent
            hoverEnabled: true
            onEntered: {
                parent.hovered = true;
                dropdownHideTimer.stop();
            }
            onExited: {
                parent.hovered = false;
                if (!locationInput.getActiveFocus())
                    dropdownHideTimer.start();
            }
            acceptedButtons: Qt.NoButton
        }

        Item {
            anchors.fill: parent
            anchors.margins: Style.spacingS

            DankListView {
                id: searchResultsList

                anchors.fill: parent
                clip: true
                model: searchResultsModel
                spacing: Style.spacingXXS

                delegate: StyledRect {
                    width: searchResultsList.width
                    height: 36
                    radius: Style.cornerRadius
                    color: resultMouseArea.containsMouse ? Style.surfaceLight : Style.withAlpha(Style.surfaceLight, 0)

                    Row {
                        anchors.fill: parent
                        anchors.margins: Style.spacingM
                        spacing: Style.spacingS

                        DankIcon {
                            name: "place"
                            size: Style.iconSize - 6
                            color: Style.surfaceVariantText
                            anchors.verticalCenter: parent.verticalCenter
                        }

                        StyledText {
                            text: model.name || "Unknown"
                            font.pixelSize: Style.fontSizeMedium
                            color: Style.surfaceText
                            anchors.verticalCenter: parent.verticalCenter
                            elide: Text.ElideRight
                            width: parent.width - 30
                        }
                    }

                    MouseArea {
                        id: resultMouseArea

                        anchors.fill: parent
                        hoverEnabled: true
                        cursorShape: Qt.PointingHandCursor
                        onClicked: {
                            root._internalChange = true;
                            const selectedName = model.name;
                            const selectedQuery = model.query;
                            locationInput.text = selectedName;
                            root.locationSelected(selectedName, selectedQuery);
                            root.resetSearchState();
                            locationInput.setFocus(false);
                            root._internalChange = false;
                        }
                    }
                }
            }

            StyledText {
                anchors.centerIn: parent
                text: root.isLoading ? "Searching..." : "No locations found"
                font.pixelSize: Style.fontSizeMedium
                color: Style.surfaceVariantText
                visible: searchResultsList.count === 0 && root.canSearch(locationInput.text)
            }
        }
    }
}
